/**
 * @description       : Get records from VasSupplierTable__c and copy their fields to related subscriptions/assets/orders
 * @author            : gabriele.rota@webresults.it
 * @group             : WR
 * @last modified on  : 2021-08-13
 * @last modified by  : gabriele.rota@webresults.it
**/
public with sharing class HDT_BA_VasSupplierTable implements Database.Batchable<SObject>, Schedulable{

    private static final String STATUS_PENDING = 'PENDING';
    private static final String STATUS_SUCCESS = 'SUCCESS';
    private static final String STATUS_ERROR = 'ERROR';

    private static final List<String> FIELDS_TO_COPY = new List<String>{
        'VasPolicyNumber__c',
        'VasSupplierStatus__c',
        'VasEffectiveDate__c',
        'VasSupplierStatusDetails__c',
        'VasSupplier__c',
        'VasPracticeDate__c',
        'VasShippingDate__c',
        'VasTrackingNr__c',
        'VasDeliveryDate__c',
        'VasServiceActivationDate__c',
        'VasCommunicationProgressive__c',
        'VasCommunicationDate__c',
        'VasReplacementRequestDate__c',
        'VasReplacementDate__c',
        'VasReturnedProductDate__c',
        'VasDisabledDate__c'
    };

    private static Map<id, id> vasSupplToOrder;

    /**
    * @description Running batch
    * @author gabriele.rota@webresults.it | 2021-08-13 
    * @param sc 
    **/
    public void execute(SchedulableContext sc){
        Database.executeBatch(this);
    }
    
    /**
    * @description Get records which have not been processed yet
    * @author gabriele.rota@webresults.it | 2021-08-13 
    * @param bc 
    * @return Database.QueryLocator 
    **/
    public Database.QueryLocator start(Database.BatchableContext bc) {
        String query = 'SELECT '+String.join(FIELDS_TO_COPY, ',')+',VasPracticeCode__c,Status__c,ErrorDescription__c';
        QUERY += ' FROM VasSupplierTable__c WHERE Status__c=:STATUS_PENDING';
        return Database.getQueryLocator(query);
    }

    /**
    * @description Copying VasSupplierTable__c records to subscriptions/assets/orders and saving outcome
    * @author gabriele.rota@webresults.it | 2021-08-13 
    * @param bc 
    * @param vasRecords 
    **/
    public void execute(Database.BatchableContext bc, List<VasSupplierTable__c> vasRecords) {
        vasSupplToOrder= new Map<Id, Id>();
        Map<String, VasSupplierTable__c> vasRecordsMap = mapVasRecords(vasRecords);
        Set<String> codes = vasRecordsMap.keySet();

        List<SBQQ__Subscription__c> subs = getSubscriptions(codes);
        List<Asset> assets = getAssets(codes);
        system.debug('subs '+subs?.size()+JSON.serialize(subs));
        system.debug('assets '+assets?.size()+JSON.serialize(assets));

        Map<SObject, String> recordsPerCode = new Map<SObject, String>();
        recordsPerCode.putAll( copyVasFields(subs, vasRecordsMap) );
        recordsPerCode.putAll( copyVasFields(assets, vasRecordsMap) );

        updateRecords(recordsPerCode, vasRecordsMap);
        
        saveVasRecords(vasRecords);
    }

    public void finish(Database.BatchableContext bc) {}


    /**
    * @description Mapping Vas records by VasPracticeCode__c
    * @author gabriele.rota@webresults.it | 2021-08-13 
    * @param vasRecords 
    * @return Map<String, VasSupplierTable__c> 
    **/
    private Map<String, VasSupplierTable__c> mapVasRecords(List<VasSupplierTable__c> vasRecords) {
        Map<String, VasSupplierTable__c> vasRecordsMap = new Map<String, VasSupplierTable__c>();
        for (VasSupplierTable__c vasRecord:vasRecords) {
            String code = vasRecord.VasPracticeCode__c;
            vasRecordsMap.put(code, vasRecord);
        }
        return vasRecordsMap;
    }

    /**
    * @description Getting subscriptions by VasPracticeCode__c
    * @author gabriele.rota@webresults.it | 2021-08-13 
    * @param codes 
    * @return List<SBQQ__Subscription__c> 
    **/
    private List<SBQQ__Subscription__c> getSubscriptions(Set<String> codes) {
        return [SELECT VasPracticeCode__c, SBQQ__OrderProduct__r.OrderId FROM SBQQ__Subscription__c WHERE VasPracticeCode__c IN :codes WITH SECURITY_ENFORCED];
    }

    /**
    * @description Getting assets by VasPracticeCode__c
    * @author gabriele.rota@webresults.it | 2021-08-13 
    * @param codes 
    * @return List<Asset> 
    **/
    private List<Asset> getAssets(Set<String> codes) {
        return [SELECT VasPracticeCode__c, SBQQ__OrderProduct__r.OrderId FROM Asset WHERE VasPracticeCode__c IN :codes WITH SECURITY_ENFORCED];
    }

    /**
    * @description Copy fields from vas records to subscriptions/assets/orders
    * @author gabriele.rota@webresults.it | 2021-08-13 
    * @param targets 
    * @param vasRecordsMap 
    **/
    private Map<SObject, String> copyVasFields(List<SObject> targets, Map<String, VasSupplierTable__c> vasRecordsMap) {
        Map<SObject, String> recordsPerCode = new Map<SObject, String>();

        if (!targets.isEmpty()) {
            Set<String> targetFieldNames = targets.get(0).getSObjectType().getDescribe().fields.getMap().keySet();
            
            for (SObject target:targets) {
                String code = (String)target.get('VasPracticeCode__c');
                VasSupplierTable__c vasRecord = vasRecordsMap.get(code);
                Order orderToUpdate = new Order();

                //mapping VasSupplierTable__c to Asset/Subscription
                try {
                    for (String field:FIELDS_TO_COPY) {
                        if (targetFieldNames.contains(field.toLowerCase())) {
                            target.put(field, vasRecord.get(field));
                        }
                    }
                } catch (Exception e) {
                    System.debug(LoggingLevel.ERROR, e.getMessage());
                    system.debug(e.getStackTraceString());
                    vasRecord.Status__c = STATUS_ERROR;
                    vasRecord.ErrorDescription__c = 'MAPPING_ERROR: Error mapping Asset/Subscription fields';
                }

                //mapping VasSupplierTable__c to Order
                try {
                    OrderItem item = (OrderItem)target.getSObject('SBQQ__OrderProduct__r');
                    orderToUpdate.Id = item.OrderId;
                    orderToUpdate.VasSupplierStatus__c = vasRecord.VasSupplierStatus__c;
                    vasSupplToOrder.put(vasRecord.Id, orderToUpdate.Id);
                } catch (Exception e) {
                    System.debug(LoggingLevel.ERROR, e.getMessage());
                    system.debug(e.getStackTraceString());
                    vasRecord.Status__c = STATUS_ERROR;
                    vasRecord.ErrorDescription__c = 'MAPPING_ERROR: Error mapping Order fields';
                }

                //saving records in map if no errors have been encountered
                if (vasRecord.Status__c==STATUS_PENDING) {
                    recordsPerCode.put(target, code);
                    recordsPerCode.put(orderToUpdate, code);
                }
            }
        }
        return recordsPerCode;
    }
   
    /**
    * @description Updating orders/assets/subscriptions
    * @author gabriele.rota@webresults.it | 2021-08-23 
    * @param recordsPerCode 
    * @param vasRecordsMap 
    **/
    private void updateRecords(Map<SObject, String> recordsPerCode, Map<String, VasSupplierTable__c> vasRecordsMap) {

        List<Asset> assetsToUpdate = new List<Asset>();
        List<SBQQ__Subscription__c> subsToUpdate = new List<SBQQ__Subscription__c>();
        //List<Order> ordersToUpdate = new List<Order>();
        Map<Id, Order> mapOrdersToUpdate = new Map<Id, Order>();

        Map<Id, String> recordIdsPerCode = new Map<Id, String>();
        for (SObject record:recordsPerCode.keySet()) {
            if (record.getSObjectType() == Schema.Asset.getSObjectType()) {
                assetsToUpdate.add((Asset)record);
            }
            if (record.getSObjectType() == Schema.SBQQ__Subscription__c.getSObjectType()) {
                subsToUpdate.add((SBQQ__Subscription__c)record);
            }
            if (record.getSObjectType() == Schema.Order.getSObjectType()) {
                //ordersToUpdate.add((Order)record);
                mapOrdersToUpdate.put((ID) record.get('id'), (Order)record);
            }
            recordIdsPerCode.put((Id)record.get('Id'), recordsPerCode.get(record) );
        }

        saveAndCheckResults(assetsToUpdate, recordIdsPerCode, vasRecordsMap);
        saveAndCheckResults(subsToUpdate, recordIdsPerCode, vasRecordsMap);
        saveAndCheckResults(mapOrdersToUpdate.values(), recordIdsPerCode, vasRecordsMap);
    }

    /**
    * @description Run orders/assets/subscriptions update and save outcome
    * @author gabriele.rota@webresults.it | 2021-08-23 
    * @param vasRecords 
    * @param recordIdsPerCode 
    * @param vasRecordsMap 
    **/
    private void saveAndCheckResults(List<SObject> records, Map<Id, String> recordIdsPerCode, Map<String, VasSupplierTable__c> vasRecordsMap) {
        List<Database.SaveResult> saveResults = Database.update(records, false);

        for (Integer i=0;i<saveResults.size();i++) {
            Database.SaveResult saveResult = saveResults.get(i);
            SObject record = records.get(i);

            String code = recordIdsPerCode.get((Id)record.get('Id'));
            VasSupplierTable__c vasRecord = vasRecordsMap.get(code);

            if (saveResult.isSuccess()) {
                if (vasRecord.Status__c==STATUS_PENDING) {
                    vasRecord.Status__c = STATUS_SUCCESS;
                    vasRecord.ErrorDescription__c = null;
                }
            }
            else {
                vasRecord.Status__c = STATUS_ERROR;
                if (vasRecord.ErrorDescription__c==null) {
                    vasRecord.ErrorDescription__c = '';
                }
                vasRecord.ErrorDescription__c += getErrorDescription(saveResult);
            }
        }
    }

    /**
    * @description Save VasSupplierTable__c records with new statuses
    * @author gabriele.rota@webresults.it | 2021-08-23 
    * @param vasRecords 
    **/
    private void saveVasRecords(List<VasSupplierTable__c> vasRecords) {
        List<String> ordsIdScarti = new List<String>();
        for (VasSupplierTable__c vasRecord:vasRecords) {
            if (vasRecord.Status__c==STATUS_PENDING) {
                vasRecord.Status__c = STATUS_ERROR;
                vasRecord.ErrorDescription__c = 'NOT_FOUND: VasPracticeCode__c not found';
            }else if(vasRecord.Status__c==STATUS_ERROR){
                ordsIdScarti.add(vasSupplToOrder.get(vasRecord.Id));
            }

            if (String.isNotBlank(vasRecord.ErrorDescription__c)) {
                vasRecord.ErrorDescription__c = vasRecord.ErrorDescription__c.abbreviate(255);
            }
        }
        Database.update(vasRecords, true);
        createDraftActivities(ordsIdScarti);
    }

    /**
    * @description Get error description from SaveResult
    * @author gabriele.rota@webresults.it | 2021-08-20 
    * @param targets 
    * @param vasRecordsMap 
    **/
    private String getErrorDescription(Database.SaveResult saveResult) {
        String errorDesc = '';
        for(Database.Error err : saveResult.getErrors()) {
            errorDesc += err.getStatusCode()+': '+err.getMessage()+' ('+err.getFields()+'); ';
        }
        return errorDesc;
    }

    /**
    * @description 
    * @author federico.defelice@webresults.it | 24-08-2021 
    * @param ordsIdScarti 
    **/
    private void createDraftActivities(List<String> ordsIdScarti){
        list<wrts_prcgvr__Activity__c> activities = HDT_UTL_Scarti.createActivityByObjectIdAndTemplateNameBulk(ordsIdScarti, 'CBS_ATT003__KO_FORNITORE');
        HDT_UTL_DatabaseService.insertSObject(activities);
    }
}

/**
 * @description       : Generating requests for activating/disactivating VAS
 * @author            : gabriele.rota@webresults.it
 * @group             : WR
 * @last modified on  : 2021-07-26
 * @last modified by  : gabriele.rota@webresults.it
 * Modifications Log 
 * Ver   Date         Author                        Modification
 * 1.0   2021-07-12   gabriele.rota@webresults.it   Initial Version
**/
public with sharing class HDT_UTL_VasManagement {

    private static final String VAS_STANDALONE_PRICEBOOK = 'VAS STANDALONE';

    /**
    * @description Generates VAS activation request
    * @author gabriele.rota@webresults.it | 2021-07-12 
    * @param orderId  
    * @return HDT_WRP_VASActivationReq 
    **/
    public static HDT_WRP_VASActivationReq getActivationRequest(Id orderId) {
        Order currentOrder = getOrderData(orderId);
        HDT_WRP_VASActivationReq actReq = new HDT_WRP_VASActivationReq(currentOrder);
        return actReq;
    }

    /**
    * @description Generates VAS deactivation request
    * @author gabriele.rota@webresults.it | 2021-07-12 
    * @param orderId 
    * @return HDT_WRP_VASActivationReq 
    **/
    public static HDT_WRP_VASDisactivationReq getDeactivationRequest(Id caseId) {
        Case currentCase = [SELECT CaseNumber, VasDeactivationCausal__c, Order__c, Asset.SBQQ__OrderProduct__r.OrderId,
            Subscription__r.SBQQ__Contract__r.SBQQ__Order__c
            FROM Case WHERE Id = :caseId WITH SECURITY_ENFORCED LIMIT 1];
        
        Id orderId = currentCase.Order__c;
        if (String.isBlank(orderId)) {
            orderId = currentCase.Asset.SBQQ__OrderProduct__r.OrderId;
        }
        if (String.isBlank(orderId)) {
            orderId = currentCase.Subscription__r.SBQQ__Contract__r.SBQQ__Order__c;
        }

        Order currentOrder = getOrderData(orderId);
        HDT_WRP_VASDisactivationReq disactReq = new HDT_WRP_VASDisactivationReq(currentOrder, currentCase);
        return disactReq;
    }

    /**
    * @description Query on Order object and related records
    * @author gabriele.rota@webresults.it | 2021-07-13 
    * @param orderId 
    * @return Order 
    **/
    private static Order getOrderData(Id orderId) {
        //TODO: move query in HDT_QR_Order class
        return [SELECT SalesCompany__c, ContractAccountCode__c, CustomerCode__c, CustomerFiscalCode__c, CustomerVATNumber__c,
            ServicePointCode__c, SalesCompanyCode__c, CustomerName__c, CustomerLastName__c, CommercialProduct__c, CommercialProductVersion__c, OrderNumber,
            VasServiceActivationDate__c, ShippingStreetToponym__c, ShippingStreetName__c, ShippingStreetNumber__c,
            ShippingCity__c, ShippingPlace__c, ShippingPostalCode__c, ShippingProvince__c, PhoneNumber__c, ShippingMail__c, SignedDate__c,
            ContractReference__r.SAPContractCode__c,
            SBQQ__Quote__r.SBQQ__ListAmount__c, SBQQ__Quote__r.SBQQ__TotalCustomerDiscountAmount__c, SBQQ__Quote__r.SBQQ__PriceBook__r.Name,
            SBQQ__Quote__r.ListAmountVAT__c, SBQQ__Quote__r.TotalDiscountAmountVAT__c,
            (SELECT Quantity, PriceNet__c, PriceNetDiscounted__c, Percentage__c, VASBillingMode__c, SBQQ__RequiredBy__c, VasActivationDate__c,
                DurationDay__c, PaymentMode__c, IVA__c,
                Product2.Name, Product2.NumberRateMonthly__c
                FROM OrderItems)
            FROM Order WHERE Id = :orderId WITH SECURITY_ENFORCED];
    }

    /**
    * @description Join given string values e.g. ["Mickey","Mouse"] => "Mickey Mouse"
    * @author gabriele.rota@webresults.it | 2021-07-13 
    * @param strValues 
    * @return String 
    **/
    private static String joinStringValues(List<String> strValues) {
        List<String> finalStrValues = new List<String>();
        for (String strValue:strValues) {
            if (String.isNotBlank(strValue)) {
                finalStrValues.add(strValue);
            }
        }
        return String.join(finalStrValues, ' ');
    }

    public virtual class HDT_WRP_VasReq extends HDT_WS_Request{
        String societa;
        String partner;
        String codFiscale;
        String partitaIva;
        String pod;
        String intestatario;
        String servizioIp;
        String versione;
        String keyServizio;
        Date dataCessazione;

        public HDT_WRP_VASReq(Order currentOrder) {
            //societa = currentOrder.SalesCompany__c;
            societa = currentOrder.SalesCompanyCode__c;
            partner = currentOrder.CustomerCode__c;
            codFiscale = currentOrder.CustomerFiscalCode__c;
            partitaIva = currentOrder.CustomerVATNumber__c;
            pod = currentOrder.ServicePointCode__c;

            List<String> strValues = new List<String>{currentOrder.CustomerName__c, currentOrder.CustomerName__c};
            intestatario = joinStringValues(strValues);

            servizioIp = currentOrder.CommercialProduct__c;
            versione = currentOrder.CommercialProductVersion__c.split('\\.\\d+')[0];
            keyServizio = currentOrder.OrderNumber;

            //dataCessazione = null
        }

        public HDT_WRP_VASReq(Order currentOrder, Case currentCase) {
            this(currentOrder);
            this.keyServizio = currentCase.CaseNumber;
        }
    }

    public class HDT_WRP_VasDisactivationReq extends HDT_WRP_VASReq{
        String nrContrattoIp;
        String numDocRif;
        String statoPratica;

        public HDT_WRP_VASDisactivationReq(Order currentOrder, Case currentCase) {
            super(currentOrder, currentCase);

            nrContrattoIp = currentOrder.ContractReference__r?.SAPContractCode__c;

            //numDocRif = null
            statoPratica = currentCase.VasDeactivationCausal__c;
        }
    }

    public class HDT_WRP_VasActivationReq extends HDT_WRP_VASReq{
        
        String contractAccountSap;
        Date dataAttivazione;
        Decimal nrRate;
        Decimal importo;
        Decimal importoIvaInclusa;
        Decimal scontoIvaInclusa;
        String indirizzoSpedizione;
        String city1;
        String postCode1;
        String region;
        String telefono;
        String mail;
        String perio;
        String modalitaFatturazione;
        Date dataDecorrenza;
        Date dataFirma;
        String vincoloPermanenza;
        Decimal durataServizioGg;
        Boolean isContCambioOfferta;
        String modalitaPagamento;
        String rfid;
        Boolean isVasStandAlone;
        Boolean isVasOmaggio;

        List<HDT_WRP_VASActivationReqItem> items;

        public HDT_WRP_VASActivationReq(Order currentOrder) {
            super(currentOrder);

            OrderItem parentOrderItem;
            items = new List<HDT_WRP_VASActivationReqItem>();
            for (OrderItem ordItem:currentOrder.OrderItems) {
                HDT_WRP_VASActivationReqItem reqItem = new HDT_WRP_VASActivationReqItem(ordItem);
                items.add(reqItem);

                if (String.isBlank(ordItem.SBQQ__RequiredBy__c)) {
                    parentOrderItem = ordItem;
                }
            }

            contractAccountSap = currentOrder.ContractAccountCode__c;
            dataAttivazione = currentOrder.VasServiceActivationDate__c;
            nrRate = parentOrderItem?.Product2?.NumberRateMonthly__c;
            importo = currentOrder.SBQQ__Quote__r?.SBQQ__ListAmount__c;

            importoIvaInclusa = currentOrder.SBQQ__Quote__r?.ListAmountVAT__c;
            scontoIvaInclusa = currentOrder.SBQQ__Quote__r?.TotalDiscountAmountVAT__c;

            List<String> strValues = new List<String>{
                currentOrder.ShippingStreetToponym__c,
                currentOrder.ShippingStreetName__c,
                currentOrder.ShippingStreetNumber__c,
                currentOrder.ShippingCity__c,
                currentOrder.ShippingPlace__c,
                currentOrder.ShippingPostalCode__c
            };
            indirizzoSpedizione = joinStringValues(strValues);

            city1 = currentOrder.ShippingCity__c;
            postCode1 = currentOrder.ShippingPostalCode__c;
            region = currentOrder.ShippingProvince__c;
            telefono = currentOrder.PhoneNumber__c;
            mail = currentOrder.ShippingMail__c;
            modalitaFatturazione = parentOrderItem?.VASBillingMode__c.substring(0,1);
            dataDecorrenza = parentOrderItem?.VasActivationDate__c;
            dataFirma = currentOrder.SignedDate__c;
            durataServizioGg = parentOrderItem?.DurationDay__c;
            modalitaPagamento = parentOrderItem?.PaymentMode__c;
            
            isVasStandAlone = (currentOrder.SBQQ__Quote__r?.SBQQ__PriceBook__r?.Name?.toUpperCase() == VAS_STANDALONE_PRICEBOOK);
            
            //perio = null
            //vincoloPermanenza = null
            //rfid = null

            //isContCambioOfferta = currentOrder.ContractReference__c ???
            //isVasOmaggio = ???
        }
    }

    public class HDT_WRP_VasActivationReqItem{
        String ItemProdotto;
        Decimal Quantita;
        Decimal PrezzoNetto;
        Decimal PrezzoNettoScontato;
        Decimal PercentualeIva;

        public HDT_WRP_VASActivationReqItem(OrderItem ordItem) {
            ItemProdotto = ordItem.Product2?.Name;
            Quantita = ordItem.Quantity;
            PrezzoNetto = ordItem.PriceNet__c;
            PrezzoNettoScontato = ordItem.PriceNetDiscounted__c;
            PercentualeIva = ordItem.Percentage__c==null? 0.0 : ordItem.Percentage__c;
        }
    }
}